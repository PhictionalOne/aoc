# [Day 13: Transparent Origami](https://adventofcode.com/2021/day/13)

You reach another volcanically active part of the cave. It would be nice if you could do
some kind of thermal imaging so you could tell ahead of time which caves are too hot to
safely enter.

Fortunately, the submarine seems to be equipped with a thermal camera! When you activate
it, you are greeted with:

```
Congratulations on your purchase! To activate this infrared thermal imaging
camera system, please enter the code found on page 1 of the manual.
```

Apparently, the Elves have never used this feature. To your surprise, you manage to find
the manual; as you go to open it, page 1 falls out. It's a large sheet of 
[transparent paper](https://en.wikipedia.org/wiki/Transparency_(projection)! The
transparent paper is marked with random dots and includes instructions on how to fold it
up (your puzzle input). For example:

```
6,10
0,14
9,10
0,3
10,4
4,11
6,0
6,12
4,1
0,13
10,12
3,4
3,0
8,4
1,10
2,14
8,10
9,0

fold along y=7
fold along x=5
```

The first section is a list of dots on the transparent paper. `0,0` represents the
top-left coordinate. The first value, `x`, increases to the right. The second value, `y`,
increases downward. So, the coordinate `3,0` is to the right of `0,0`, and the coordinate
`0,7` is below `0,0`. The coordinates in this example form the following pattern, where
`#` is a dot on the paper and `.` is an empty, unmarked position:

```
...#..#..#.
....#......
...........
#..........
...#....#.#
...........
...........
...........
...........
...........
.#....#.##.
....#......
......#...#
#..........
#.#........
```

Then, there is a list of **fold instructions**. Each instruction indicates a line on the
transparent paper and wants you to fold the paper **up** (for horizontal `y=...` lines) or
**left** (for vertical `x=...` lines). In this example, the first fold instruction is fold
along `y=7`, which designates the line formed by all of the positions where `y` is `7`
(marked here with `-`):

```
...#..#..#.
....#......
...........
#..........
...#....#.#
...........
...........
-----------
...........
...........
.#....#.##.
....#......
......#...#
#..........
#.#........
```

Because this is a horizontal line, fold the bottom half **up**. Some of the dots might end
up overlapping after the fold is complete, but dots will never appear exactly on a fold
line. The result of doing this fold looks like this:

```
#.##..#..#.
#...#......
......#...#
#...#......
.#.#..#.###
...........
...........
```

Now, only `17` dots are visible.

Notice, for example, the two dots in the bottom left corner before the transparent paper
is folded; after the fold is complete, those dots appear in the top left corner (at `0,0`
and `0,1`). Because the paper is transparent, the dot just below them in the result (at
`0,3`) remains visible, as it can be seen through the transparent paper.

Also notice that some dots can end up **overlapping**; in this case, the dots merge
together and become a single dot.

The second fold instruction is `fold along x=5`, which indicates this line:

```
#.##.|#..#.
#...#|.....
.....|#...#
#...#|.....
.#.#.|#.###
.....|.....
.....|.....
```

Because this is a vertical line, fold **left**:

```
#####
#...#
#...#
#...#
#####
.....
.....
```

The instructions made a square!

The transparent paper is pretty big, so for now, focus on just completing the first fold.
After the first fold in the example above, **`17`** dots are visible - dots that end up
overlapping after the fold is completed count as a single dot.

**How many dots are visible after completing just the first fold instruction on your
transparent paper?**


## Part Two
Finish folding the transparent paper according to the instructions. The manual says the
code is always **eight capital letters**.

**What code do you use to activate the infrared thermal imaging camera system?**

```haskell
import Data.List
import Data.List.Split


type Point = (Int, Int)
type FoldInstruction = (Char, Int)

foldPaper :: Int -> [String] -> [Point]
foldPaper n cmds = map head . group . sort            -- Distinct points
                 . foldr map points . map foldPoint   -- Apply n fold
                 . reverse $ take n folds             -- instruction
  where [ps, fs] = splitOn [""] cmds

        points = map parsePoint ps
        folds  = map parseFold fs
        
        -- | For Points of input String 
        --
        -- "1141,761"
        --
        parsePoint :: String -> Point
        parsePoint input = (x,y)
          where [x,y] = map toInt $ splitOn "," input

        -- | For Folding instruction input
        --
        -- "fold along x=655"
        --
        parseFold :: String -> FoldInstruction
        parseFold input = (head axis, toInt step)
          where [axis, step] = splitOn "=" 
                             . last 
                             $ splitOn " " input

        toInt :: String -> Int
        toInt x = read x :: Int

        -- | FoldInstruction Points along an axis
        --      
        --      oooooo|ooox        ooxooo|
        --      ↑     ↑   ↑   ==>  ↑ ↑   ↑
        --      0    h=6 x=10      0 x' h=6
        --
        foldPoint :: FoldInstruction -> Point -> Point
        foldPoint ('x', h) (x, y) | h >= x    = (x,     y)
                                  | otherwise = (h-x+h, y)
        foldPoint ('y', v) (x, y) | v >= y    = (x,     y)
                                  | otherwise = (x, v-y+v)

-- | Turn a list of indices into a String of marks
--                   0123456
--    [0,1,2,6] ==> "###   #"
--
showLn :: [Int] -> String
showLn = showLn' (-1)
  where showLn' :: Int -> [Int] -> String
        showLn'    _       []                       = ""
        showLn' prev s@(x:xs) | abs (x - prev) == 1 = '#' : showLn' x xs
                              | otherwise           = ' ' : showLn' (prev + 1) s


-- Example from Task
examplePointList :: [Point]
examplePointList = [ (6,10), (0,14), (9,10), (0, 3), (10, 4), (4,11)
                   , (6, 0), (6,12), (4, 1), (0,13), (10,12), (4, 4)
                   , (3, 0), (8, 4), (1,10), (2,14), ( 8,10), (9, 0) ]

exampleFoldList :: [FoldInstruction]
exampleFoldList = [ ('y', 7), ('x', 5) ]


-- Parts
partOne :: String -> Int
partOne = length . foldPaper 1 . lines
        
partTwo :: String -> String
partTwo = foldr (\x xs -> x ++ "\n" ++ xs) ""
        . map (showLn . map fst)
        . groupBy (\x y -> snd x == snd y)  . sortOn snd 
        . foldPaper 1000
        . lines


-- Main function
main :: IO ()
main = do
  puzzle <- readFile "puzzle.aoc"
  putStrLn $
    "How many dots are visible after completing just the first fold instruction\n"
     ++ "on your transparent paper?"
  print $ partOne puzzle
  putStrLn 
    "What code do you use to activate the infrared thermal imaging camera system?"
  putStrLn $ partTwo puzzle
```
